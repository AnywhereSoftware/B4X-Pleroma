Build1=Default,b4i.pleroma,HU2_PUBLIC
Build2=Simulator,b4i.pleroma,HU2_PUBLIC,SIMULATOR
File1=AccountView.bil
File10=icon.png
File11=LeftDrawer.bil
File12=lstDrawerUser.bil
File13=MainPage.bil
File14=MiniAccountView.bil
File15=Missing-image-232x150.png
File16=nsfw.74818f9.png
File17=play.png
File18=PostView.bil
File19=PostView.json
File2=AccountView.json
File20=PostViewMedia.bil
File21=ReactionsView.bil
File22=Report.json
File23=Search.bil
File24=Settings.json
File25=StatusList.bil
File26=StatusViewImpl.bil
File27=theme.json
File28=VideoPlayer.bil
File29=WebViewDialog.bil
File3=CardView.bil
File4=close_icon.png
File5=DialogContainer.bil
File6=empty.png
File7=GifView.bil
File8=hamburger.png
File9=hamburger_notif.png
FileGroup1=Default Group
FileGroup10=Default Group
FileGroup11=Default Group
FileGroup12=Default Group
FileGroup13=Default Group
FileGroup14=Default Group
FileGroup15=Default Group
FileGroup16=Default Group
FileGroup17=Default Group
FileGroup18=Default Group
FileGroup19=Default Group
FileGroup2=Default Group
FileGroup20=Default Group
FileGroup21=Default Group
FileGroup22=Default Group
FileGroup23=Default Group
FileGroup24=Default Group
FileGroup25=Default Group
FileGroup26=Default Group
FileGroup27=Default Group
FileGroup28=Default Group
FileGroup29=Default Group
FileGroup3=Default Group
FileGroup4=Default Group
FileGroup5=Default Group
FileGroup6=Default Group
FileGroup7=Default Group
FileGroup8=Default Group
FileGroup9=Default Group
Group=Default Group
Library1=icore
Library10=zoomimageview
Library11=webp
Library12=keyvaluestore
Library13=b4xdrawer
Library14=b4xpreferencesdialog
Library15=bctoast
Library16=ifirebaseanalytics
Library17=imedia
Library18=iphone
Library19=ifirebasenotifications
Library2=b4xpages
Library20=ihttputils2
Library21=iui9
Library22=iwebsocket
Library3=xui views
Library4=bctextengine
Library5=ijson
Library6=iui8
Library7=b4xgifview
Library8=minihtmlparser
Library9=bitmapsasync
Module1=|relative|..\AccountView
Module10=|relative|..\ListOfStatuses
Module11=|relative|..\MediaChooser
Module12=|relative|..\MiniAccountView
Module13=|relative|..\OAuth
Module14=|relative|..\PleromaFeed
Module15=|relative|..\PLMSettings
Module16=|relative|..\PostView
Module17=|relative|..\Push
Module18=|relative|..\ReactionsView
Module19=|relative|..\ReportManager
Module2=|relative|..\B4XLinksManager
Module20=|relative|..\RequestsManager
Module21=|relative|..\SearchManager
Module22=|relative|..\ServerManager
Module23=|relative|..\StackManager
Module24=|relative|..\StatusView
Module25=|relative|..\Streamer
Module26=|relative|..\StubView
Module27=|relative|..\TextUtils
Module28=|relative|..\ThemeManager
Module29=UserNotificationCenter
Module3=|relative|..\B4XMainPage
Module30=|relative|..\ViewsCache
Module31=|relative|..\WebViewDialog
Module4=|relative|..\BBListItem
Module5=|relative|..\CardView
Module6=|relative|..\Constants
Module7=|relative|..\DrawerManager
Module8=|relative|..\HtmlToRuns
Module9=|relative|..\ImagesCache
NumberOfFiles=29
NumberOfLibraries=22
NumberOfModules=31
Version=6.8
@EndOfDesignText@
'Code module
#Region  Project Attributes 
	#ApplicationLabel: B4X Pleroma
	#Version: 1.29.0
	'Orientation possible values: Portrait, LandscapeLeft, LandscapeRight and PortraitUpsideDown
	#iPhoneOrientations: Portrait
	#iPadOrientations: Portrait
	#Target: iPhone, iPad
	#ATSEnabled: false
	#MinVersion: 10
	#PlistExtra: <key>UIViewControllerBasedStatusBarAppearance</key><false/>
	#UrlScheme: b4i.pleroma
	#if Not(SIMULATOR)
	#AdditionalLib: KSCrash.framework.3
	#AdditionalLib: MessageUI.framework
	#AdditionalLib: SystemConfiguration.framework
	#AdditionalLib: libc++.dylib
	#AdditionalLib: libz.dylib
	#End If
	#PlistExtra:<key>NSPhotoLibraryUsageDescription</key><string>Add images and videos as attachments.</string>
	#PlistExtra:<key>NSCameraUsageDescription</key><string>Add images and videos as attachments.</string>
	#PlistExtra:<key>NSMicrophoneUsageDescription</key><string>Add videos as attachments.</string>
	#ProvisionFile: pleroma.mobileprovision
	#Entitlement: <key>aps-environment</key><string>production</string>
	#if Not(SIMULATOR)
'	#ProvisionFile: pleroma_store.mobileprovision
	#End If
	'This enables the system dark mode feature.
	#PlistExtra: <!--<key>UIUserInterfaceStyle</key>-->
#End Region

Sub Process_Globals
	Public App As Application
	Public NavControl As NavigationController
	Private reporter As NativeObject
	Private xui As XUI
	Private Analytics As FirebaseAnalytics
	Public fm As FirebaseMessaging
	Private ForegroundFlag As Boolean 'https://www.b4x.com/android/forum/threads/re-how-detect-user-tap-on-notification.117548/#content
	Private unc As UserNotificationCenter
	Public Const NavBarBarTintColor As Int = 0xFF006FA6
	Public Const NavBarTintColor As Int = Colors.White
	Private BackgroundTime As Long 'this is used to ignore the dark state event that occurs immediately after moving to the background
End Sub

Private Sub Application_Start (Nav As NavigationController)
	SetDebugAutoFlushLogs(True)
	NavControl = Nav
	Analytics.Initialize 
	Dim PagesManager As B4XPagesManager
	PagesManager.Initialize(NavControl)
	NavControl.NavigationBarVisible = True
	Dim no As NativeObject = NavControl
	no.GetField("navigationBar").RunMethod("setBarTintColor:", Array(no.ColorToUIColor(NavBarBarTintColor)))
	Dim no As NativeObject = App
	no.RunMethod("setStatusBarStyle:", Array(1))
	SetTitleColor(Nav, Colors.White)
	Dim no As NativeObject = NavControl
	no.GetField("navigationBar").RunMethod("setTintColor:", Array(no.ColorToUIColor(NavBarTintColor)))
	#if RELEASE
	If App.IsSimulator = False Then
		CreateReporter
		SendReportsIfNeeded
	End If
	#end if
	fm.Initialize("fm")	
End Sub

Private Sub fm_FCMConnected
	Log("FCMConnected")
	'here we can subscribe and unsubscribe from topics
End Sub

Private Sub Application_RemoteNotification (Message As Map, CompletionHandler As CompletionHandler)
	If ForegroundFlag = False Then
		Log("Notification clicked")
		Sleep(100)
		If B4XPages.IsInitialized And B4XPages.MainPage.IsInitialized Then
			B4XPages.MainPage.NotificationClicked
		End If
	Else
		Log($"Message arrived: ${Message}"$)
		If unc.IsInitialized = False Then unc.Initialize
		unc.CreateNotificationWithContent(Message.Get("title"), Message.Get("body"), Message.Get("gcm.message_id"), "", 1)
		CompletionHandler.Complete
	End If
End Sub

Private Sub TraitCollection_Changed
	Dim delta As Long = DateTime.Now - BackgroundTime
	Log("TraitCollection_Changed: " & delta)
	If delta < 1000 Then Return
	If B4XPages.IsInitialized And B4XPages.MainPage.IsInitialized And B4XPages.MainPage.Theme.IsInitialized Then
		B4XPages.MainPage.Theme.SetDark(IsDark)
	End If
End Sub

Public Sub IsDark As Boolean
	Dim App As Application
	If App.OSVersion >= 13 Then
		Dim no As NativeObject = App.KeyController
		Return 2 = no.GetField("traitCollection").GetField("userInterfaceStyle").AsNumber
	End If
	Return False
End Sub

Private Sub Application_Active
	fm.FCMConnect 'should be called from Application_Active
	If B4XPages.IsInitialized And B4XPages.MainPage.IsInitialized And B4XPages.MainPage.Stream.IsInitialized Then
		B4XPages.MainPage.Stream.Disconnect
	End If
	Sleep(5000)
	ForegroundFlag = True
	App.ApplicationIconBadgeNumber = 0
End Sub



Sub Application_PushToken (Success As Boolean, Token() As Byte)
	Log($"PushToken: ${Success}"$)
	Log(LastException)
End Sub

Sub SendReportsIfNeeded As ResumableSub
	Dim no As NativeObject
	no = no.Initialize("KSCrash").RunMethod("sharedInstance", Null)
	Dim reports As Int = no.GetField("reportCount").AsNumber
	Log($"Number of reports: ${reports}"$)
	'Page1.Title = reports
	If reports > 0 Then
		Sleep(0)
		Dim nme As NativeObject = Me
		nme.RunMethod("sendReports:", Array(reporter))
	End If
	Return True
End Sub

Sub CreateReporter
	reporter = reporter.Initialize("KSCrashInstallationStandard").RunMethod("sharedInstance", Null)
	Dim url As NativeObject
	url = url.Initialize("NSURL").RunMethod("URLWithString:", Array(Constants.CrashReportsServer)) '"Array("https://b4x.com:51051/report"))
	reporter.SetField("url", url)
	reporter.RunMethod("install", Null)
End Sub

#if Not(SIMULATOR)
#if OBJC
#import <KSCrash/KSCrashFramework.h>

- (void) sendReports:(KSCrashInstallation*)installation {
[installation sendAllReportsWithCompletion:^(NSArray* reports, BOOL completed, NSError* error)
     {
         if(completed)
         {
             NSLog(@"Sent %d reports", (int)[reports count]);
         }
         else
         {
             NSLog(@"Failed to send reports: %@", error);
         }
     }];
}
#End If
#End If

Sub SetTitleColor(nav As NavigationController, clr As Int)
	Dim attributes As NativeObject
	attributes = attributes.Initialize("B4IAttributedString").RunMethod("createAttributes::", _
     Array(Font.CreateNew(18), attributes.ColorToUIColor(clr)))
	Dim no As NativeObject = nav
	no.GetField("navigationBar").RunMethod("setTitleTextAttributes:", Array(attributes))
End Sub

Sub Application_OpenUrl (Url As String, Data As Object, SourceApplication As String) As Boolean

	
	B4XPages.MainPage.auth.CallFromOpenUrl(Url)
	Return True
End Sub

'Template version: B4i-1.0
#Region Delegates
Private Sub Application_Background
	B4XPages.Delegate.Activity_Pause
	fm.FCMDisconnect
	ForegroundFlag = False
	BackgroundTime = DateTime.Now
End Sub

Private Sub Application_Foreground
	B4XPages.Delegate.Activity_Resume
End Sub
#End Region

#if OBJC
@end
@interface B4IPanelView  (touchcancelled)
@end
@implementation B4IPanelView  (touchcancelled)

- (void)touchesCancelled:(NSSet *)touches withEvent:(UIEvent *)event {
		B4I* bi = [self valueForKey:@"bi"];
		NSString* eventName = [self valueForKey:@"eventName"];
		for (UITouch *u in touches) {
            CGPoint p = [u locationInView:self];
            [bi raiseEvent:self event:eventName params:@[@4, @(p.x), @(p.y)]];
        }
}
#End If

'dark mode event
#if OBJC
@end
@interface UIWindow (DarkMode)
@end
@implementation UIWindow (DarkMode)
- (void)traitCollectionDidChange:(UITraitCollection *)previousTraitCollection {
	if (self.class == NSClassFromString(@"UIWindow"))
		[[b4i_main new].bi raiseEvent:nil event:@"traitcollection_changed" params:nil];
}
#End If
